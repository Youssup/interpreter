; ex0801.a  Pass by value
startup:  bl main
          halt
;==============================================================
                           ; #include <stdio.h>
x:        .word 7          ; int x = 7;
                           ; void f(int a)
f:        push lr          ; {
          push fp
          mov fp, sp

          ldr r0, fp, 2    ;    a = a + 1;
          add r0, r0, 1
          str r0, fp, 2

          mov sp, fp       ;    return;
          pop fp
          pop lr
          ret
                           ; }
   ;===========================================================
                           ; int main()
main:     push lr          ; {
          push fp
          mov fp, sp

          ld r0, x         ;    f(x);
          push r0
          bl f
          add sp, sp, 1

          ld r0, x         ;    printf("%d\n", x);
          dout r0
          nl

          mov r0, 0        ;    return 0;
          mov sp, fp
          pop fp
          pop lr
          ret
                           ; }
