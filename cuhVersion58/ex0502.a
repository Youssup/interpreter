; ex0502.a  Dynamic local variables

startup:  bl main
          halt
;===========================================================
                            ; #include <stdio.h>
f:        push lr           ; void f(int a)
          push fp           ; {
          mov fp, sp

          mov r0, 1         ;    int x = 1, y;
          push r0
          sub sp, sp, 1

          ldr r0, fp, -1    ;    y = x + a;
          ldr r1, fp, 2
          add r0, r0, r1
          str r0, fp, -2

          ldr r0, fp, -2    ;    printf("%d\n", y);
          dout r0
          nl

          mov sp, fp        ; }
          pop fp
          pop lr
          ret
;===========================================================
main:     push lr           ; int main()   
          push fp           ; {
          mov fp, sp

          mov r0, 11        ;    f(11);
          push r0
          bl f
          add sp, sp, 1
          
          mov r0, 0         ;    return 0;
          mov sp, fp
          pop fp
          pop lr
          ret
                            ; }
