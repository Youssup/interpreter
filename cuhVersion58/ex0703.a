; ex0703.a  Pointers to functions
startup:  bl main
          halt
;==============================================================
                           ; #include <stdio.h>
sum:      .word 0          ; int sum;
p:        .word 0          ; int (*p)(int, int);
;==============================================================
f:        push lr          ; int f(int x, int y)
          push fp          ; {
          mov fp, sp

          ldr r0, fp, 2    ;    return x+y;
          ldr r1, fp, 3
          add r0, r0, r1
          mov sp, fp
          pop fp
          pop lr
          ret
                           ; }
;==============================================================
main:     push lr          ; int main()
          push fp          ; {
          mov fp, sp

          mov r0, 2        ;    sum = f(1, 2);
          push r0
          mov r0, 1
          push r0
          bl f
          add sp, sp, 2
          st r0, sum


          ld r0, sum       ;    printf("%d\n", sum);
          dout r0
          nl

          lea r0, f        ;    p = f;
          st r0, p

          mov r0, 2        ;    sum = p(1, 2);
          push r0
          mov r0, 1
          push r0
          ld r0, p
          blr r0
          add sp, sp, 2
          st r0, sum

          ld r0, sum       ;    printf("%d\n", sum);
          dout r0
          nl

          mov r0, 0        ;    return 0;
          mov sp, fp
          pop fp
          pop lr
          ret
                           ; }