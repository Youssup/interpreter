; ex1501.a  Structs in C++
startup:  bl main
          halt
;==============================================================
                           ; #include <iostream>
                           ; using namespace std;
                           ; struct Point
                           ; {
                           ;    int x;
                           ;    int y;
                           ; };
                           
@set$p1Aii:                ; void set(A *r, int n, int m)
          push lr          ; {
          push fp
          mov fp, sp
                           
          ldr r0, fp, 3    ;    r->x = n;
          ldr r1, fp, 2
          str r0, r1, 0
                           
          ldr r0, fp, 4    ;    r->y = m;
          ldr r1, fp  2
          str r0, r1, 1
                           
          mov sp, fp       ; }
          pop fp
          pop lr
          ret

@display$p1A:              ; void display(A *r)
          push lr          ; {
          push fp
          mov fp, sp
                           
          ldr r0, fp, 2   ;    cout << r->x << endl;
          ldr r0, r0, 0
          dout r0
          nl
                           
          ldr r0, fp, 2   ;    cout << r->y << endl;
          ldr r0, r0, 1
          dout r0
          nl
                           
          mov sp, fp       ; }
          pop fp
          pop lr
          ret
;==============================================================
main:     push lr           ; int main()
          push fp           ; {         
          mov fp, sp
                           
          sub sp, sp, 2    ;    A a, b;
          sub sp, sp, 2
                            
          mov r0, 6         ;    set(&a, 5, 6);
          push r0
          mov r0, 5
          push r0
          add r0, fp, -2
          push r0
          bl @set$p1Aii
          add sp, sp, 3
                            
          add r0, fp, -2    ;    display(&a);
          push r0
          bl @display$p1A
          add sp, sp, 1

          mov r0, 11        ;    set(&b, 10, 11);
          push r0
          mov r0, 10
          push r0
          add r0, fp, -4
          push r0
          bl @set$p1Aii
          add sp, sp, 3
                             
          add r0, fp, -4    ;    display(&b);
          push r0
          bl @display$p1A
          add sp, sp, 1
                            
          mov r0, 0         ;    return 0;
          mov sp, fp
          pop fp
          pop lr
          ret
                           ; } 
